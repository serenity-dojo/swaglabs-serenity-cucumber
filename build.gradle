buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.serenity-bdd:serenity-single-page-report:4.2.16"
    }
}

plugins {
    id 'java'
    id 'net.serenity-bdd.serenity-gradle-plugin' version '4.2.16'
}

group = 'net.serenitybdd.swaglabs'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '16'
targetCompatibility = '16'

repositories {
    mavenCentral()
}

ext {
    serenityVersion = '4.2.16'
    junitVersion = '5.11.4'
    junitPlatformVersion = '1.11.4'
    cucumberVersion = '7.20.1'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    testImplementation "net.serenity-bdd:serenity-core:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-cucumber:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-screenplay:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-screenplay-webdriver:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-ensure:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-saucelabs:${serenityVersion}"

    testImplementation "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-suite:${junitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-suite-api:${junitPlatformVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"

    testImplementation "io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}"

    implementation 'ch.qos.logback:logback-classic:1.2.10'
    testImplementation 'org.assertj:assertj-core:3.22.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperties System.properties
}

tasks.withType(Test) {
    systemProperty "cucumber.filter.tags", System.getProperty("tags", "")
    systemProperty "webdriver.base.url", System.getProperty("webdriver.base.url", "")
    systemProperty "browserstack.user", System.getProperty("browserstack.user", "")
    systemProperty "browserstack.key", System.getProperty("browserstack.key", "")
}

gradle.startParameter.continueOnFailure = true

serenity {
    reports = ["single-page-html"]
    testRoot = "net.serenitybdd.swaglabs"
    requirementsDir = "src/test/resources/features"
}

test.finalizedBy(aggregate,reports)